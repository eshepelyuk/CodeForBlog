buildscript {
    ext {
        springBootVersion = '1.3.6.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.bmuschko:gradle-docker-plugin:3.0.1"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'spring-boot'
    apply plugin: 'application'
    apply plugin: "com.bmuschko.docker-java-application"

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Brixton.SR3'
        }
    }

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-security'


        compile 'org.springframework.cloud:spring-cloud-starter-zuul'
        compile 'org.springframework.cloud:spring-cloud-security'
        compile 'org.springframework.security.oauth:spring-security-oauth2'

        compile group: 'org.springframework.security', name: 'spring-security-jwt'
    }

    docker {
        url = System.env.DOCKER_HOST
        certPath = new File(System.getenv('DOCKER_CERT_PATH'))
        javaApplication {
            tag = "eshepelyuk/${project.name}:latest"
        }
    }
}
project(':zuul') {
    mainClassName = "ua.eshepelyuk.oauth.ZuulMain"
    docker {
        javaApplication {
            port = 7070
        }
    }
}

project(':auth') {
    mainClassName = 'ua.eshepelyuk.oauth.AuthMain'
    docker {
        javaApplication {
            port = 8080
        }
    }
}

project(':resource') {
    mainClassName = "ua.eshepelyuk.oauth.ResourceMain"
}

